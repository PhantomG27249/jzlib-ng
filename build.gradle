plugins {
    id 'java'
    id 'c'
}

group 'io.github.duplexsystem'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

}

task resetZlibNG() {
    doLast {
        exec {
            workingDir 'zlib-ng'

            commandLine 'git', 'reset', '--hard', 'origin/stable'
        }
    }
}

task patchZlibNG() {
    dependsOn resetZlibNG
    doLast {
        exec {
            workingDir 'zlib-ng'

            commandLine 'git', 'apply', '--ignore-whitespace', '../zlib-ng-patches/0001-jlibz-ng-changes.patch'
        }
    }
}

task configZlibNG() {
    dependsOn patchZlibNG
    doLast {
        exec {
            workingDir 'zlib-ng'

            commandLine 'cmake', '.', '-DZLIB_COMPAT=true', '-DZLIB_ENABLE_TESTS=false'
        }
    }
}

task buildZlibNG() {
    dependsOn configZlibNG
    doLast {
        exec {
            workingDir 'zlib-ng'

            commandLine 'cmake', '--build', '.', '--config', 'Release'
        }
    }
}

task copyZlibNG() {
    dependsOn buildZlibNG
    doLast {
        copy {
            from 'zlib-ng/bin'
            into 'src/main/resources/native'
        }
    }
}

model {
    platforms {
        x64 {
            architecture "x64"
        }
    }

    components {
        jzlibng(NativeLibrarySpec) {
            binaries.all {
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                    cCompiler.args '-I', "${project.buildDir.toString()}/zlib-ng"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                    cCompiler.args '-I', "${project.buildDir.toString()}/zlib-ng"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64 -fomit-frame-pointer -O2'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cCompiler.args "-I${org.gradle.internal.jvm.Jvm.current().javaHome}/include/win32"
                    cCompiler.args '-I', "${project.buildDir.toString()}/zlib-ng"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/freebsd"
                    cCompiler.args '-I', "${project.buildDir.toString()}/zlib-ng"
                }
            }
        }
    }
}

assemble.dependsOn 'copyZlibNG'
copyZlibNG.dependsOn 'jzlibngSharedLibrary'
copyZlibNG.doLast{
    copy {
        from 'build/libs/jzlibng/shared'
        into 'src/main/resources/native'
    }
}


test {
    useJUnitPlatform()
}